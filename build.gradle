plugins {
    id 'war'
}

repositories {
    jcenter()
}

sourceCompatibility = 11
targetCompatibility = 11
group = 'info.serdroid.pergamon'

dependencies {
	compileOnly group: 'com.sun.xml.ws', name: 'jaxws-rt', version: '2.3.3'
	compileOnly group: 'jakarta.platform', name: 'jakarta.jakartaee-web-api', version: '8.0.0'

	testImplementation group: 'junit', name: 'junit', version: '4.13' 
	testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.15.0'

	testCompileOnly group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version: '3.3.6'
        
    testImplementation group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: '1.6.0.Final'
	testImplementation group: 'org.apache.tomee', name: 'arquillian-tomee-embedded', version: '8.0.1'
	
    testImplementation group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-api', version: '3.1.4'
    testImplementation group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-spi', version: '3.1.4'
	testImplementation group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-impl-gradle', version: '3.1.4'
	testImplementation group: 'org.gradle', name: 'gradle-tooling-api', version: '6.4.1'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.6.28'
	
}

configurations {
    jaxws
}

dependencies {
	jaxws group: 'com.sun.xml.ws', name: 'jaxws-tools', version: '2.3.3'
}


task wsgen(type: JavaExec) {
	def genDir = "src/main/generated"
    
    doFirst {
        mkdir genDir
    }
	inputs.files sourceSets.main.runtimeClasspath.asPath
	outputs.dir 'src/main/resources/wsdl'
    classpath configurations.jaxws.asPath
    main = "com.sun.tools.ws.WsGen"
    args "-wsdl",
        "-keep",
        "-cp", sourceSets.main.runtimeClasspath.asPath ,
        "-s", genDir,
        "-d", genDir,
        "-r", "src/main/resources/wsdl",
        "-Xnocompile",
        "-inlineSchemas",
        "-extension",
        "-encoding", "UTF-8",
        "info.serdroid.pergamon.ws.CalculatorWS"
}

task wsimport(type: JavaExec) {
	def genDir = "src/main/generated"
    
    doFirst {
        mkdir genDir
    }
	dependsOn 'wsgen'
	inputs.file 'src/main/resources/wsdl/CalculatorWebService.wsdl'
	outputs.dir 'src/main/java/info/serdroid/pergamon/ws/client'
    classpath configurations.jaxws.asPath
    main = "com.sun.tools.ws.WsImport"
    args "-target", "2.0",
        "-s", "src/main/java",
        "-d", genDir,
        "-keep",
        "-p", "info.serdroid.pergamon.ws.client",
        "-Xnocompile",
        "-extension",
        "-encoding", "UTF-8",
        "-wsdllocation", "src/main/resources/wsdl/CalculatorWebService.wsdl",
        "src/main/resources/wsdl/CalculatorWebService.wsdl"
}

compileTestJava.dependsOn wsimport
